// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/5/CPU.hdl
/**
 * The Hack Central Processing unit (CPU).
 * Parses the binary code in the instruction input and executes it according to the
 * Hack machine language specification. In the case of a C-instruction, computes the
 * function specified by the instruction. If the instruction specifies to read a memory
 * value, the inM input is expected to contain this value. If the instruction specifies
 * to write a value to the memory, sets the outM output to this value, sets the addressM
 * output to the target address, and asserts the writeM output (when writeM = 0, any
 * value may appear in outM).
 * If the reset input is 0, computes the address of the next instruction and sets the
 * pc output to that value. If the reset input is 1, sets pc to 0.
 * Note: The outM and writeM outputs are combinational: they are affected by the
 * instruction's execution during the current cycle. The addressM and pc outputs are
 * clocked: although they are affected by the instruction's execution, they commit to
 * their new values only in the next cycle.
 */
CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:

    // Whoever is reading this, I wrote this without
    // looking at the diagram in the book B)
    // (until I had an issue with PC hehe)

    // I write hardware like a software engineer
    // optimization can be done later
    Not(in=instruction[15], out=isAinstruction);
    Not(in=isAinstruction, out=isCinstruction);

    // load A if it's A instruction or C instruction wants to
    Or(a=isAinstruction, b=destA, out=loadA);
    Mux16(a=aluOut, b[0..14]=instruction[0..14], b[15]=false, sel=isAinstruction, out=inA);
    ARegister(in=inA, load=loadA, out[0..14]=registerA, out[0..14]=addressM);

    // comp
    Mux16(a=registerD , b= false, sel=false , out= aluX);
    Mux16(a[0..14]=registerA, a[15]=false, b=inM, sel=instruction[12], out= aluY);
    ALU(x=aluX, y=aluY, zx=instruction[11] , nx=instruction[10] , zy=instruction[9] , ny=instruction[8] , f=instruction[7] , no=instruction[6],out=aluOut, out=outM, zr=isZero, ng=isNegative);

    // D register
    And(a=isCinstruction, b=instruction[4], out=destD);
    // this was previously a regular register
    // DRegister vs Register??????
    DRegister(in=aluOut, load=destD, out=registerD);

    // (c-instruction & instruction[5]) || !c-instruction)
    // == instruction[5] || !c-instruction
    // but I'll keep this for readability
    And(a=isCinstruction, b=instruction[5], out=destA);

    // Write to memory
    And(a=isCinstruction, b=instruction[3], out=writeM);

    // jump conditions
    Not(in=isNegative,out=isPositive);
    And(a=isPositive,b=instruction[0], out=j1);
    And(a=isZero,b=instruction[1], out=j2);
    And(a=isNegative,b=instruction[2], out=j3);

    Or(a=j1, b=j2, out=j12);
    Or(a=j12, b=j3, out=j123);
    And(a=j123, b=isCinstruction, out=jump);

    // Instruction increment
    Not(in=reset, out=inc1);
    Not(in=jump, out=inc2);
    Or(a=inc1, b=inc2, out=inc);
	PC(in[0..14]=registerA, load=jump, inc=inc, reset=reset , out[0..14]=pc);
}
